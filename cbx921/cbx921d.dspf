     **-----------------------------------------------------------------------**
     **                                                                       **
     **   Compile options:                                                    **
     **     CrtDspF   File( QGPL/CBX921D )    +                               **
     **               SrcFile( QDDSSRC )      +                               **
     **               RstDsp( *YES )          +                               **
     **               WaitRcd( 3 )            +                               **
     **                                                                       **
     **   Programmer's note:                                                  **
     **     WAITRCD specifies the number of seconds that the message          **
     **     window will remain on the screen before it is closed.             **
     **                                                                       **
     **-----------------------------------------------------------------------**
     A                                      DSPSIZ(24 80 *DS3  27 132 *DS4)
     A                                      PRINT(*LIBL/QSYSPRT)
     A                                      VLDCMDKEY(15)
     A                                      ERRSFL
     A                                      CA06
     A                                      CA07
     A                                      CA08
     A                                      CA09
     A                                      CA10
     A                                      CA11
     **
     A          R ASSUME
     A                                      ASSUME
     A                                      OVERLAY
     A                                  2  5' '
     **
     A          R WDW001
     A                                      CHGINPDFT
     A  *DS3                                WINDOW(&WDWLIN 4 8 70 *NORSTCSR)
     A  *DS4                                WINDOW(&WDWLIN 30 8 70 *NORSTCSR)
     A                                      WDWBORDER((*COLOR WHT) (*DSPATR HI))
     A                                      WDWTITLE((*TEXT &WDWTTL) (*COLOR WH-
     A                                      T) *CENTER)
     A  19                                  DSPMOD(*DS4)
     A                                      KEEP
     A                                      INVITE
     A                                      OVERLAY
     A                                      USRRSTDSP
     A            WDWLIN         3S 0P
     A            WDWTTL        20A  P
     A            W1JBNM        10A  O  1  1
     A            W1MSTP        10A  O  1 12
     A            W1MSSV         2A  O  1 23
     A            W1MSID         7A  O  1 45
     A            W1DATE         8A  O  1 54
     A            W1TIME         8A  O  1 63
     A                                  2 63SYSNAME
     A                                  3  1'From  . . :'
     A            W1USPF        10A  O  3 14
     A            W1UPTX        46A  O  3 25
     A                                  4  1'Message . :'
     A            W1MSG        114A  B  4 14CNTFLD(057)
     A                                      DSPATR(PR)
     A                                      DSPATR(HI)
     A                                  7  1'F6=WrkMsg   F7=WrkJob   F8=Suspend-
     A                                         F9=Keep   F10=Reply   F11=DltMsg'
     A                                      COLOR(BLU)
